analysis_container="/cvmfs/unpacked.cern.ch/gitlab-registry.cern.ch/cms-cmu/coffea4bees:latest"
combine_container="/cvmfs/unpacked.cern.ch/gitlab-registry.cern.ch/cms-analysis/general/combine-container:CMSSW_11_3_4-combine_v9.1.0-harvester_v2.1.0"

OUTPUT_DIR="output/stat_analysis/new_trigSyst"
INPUT_DIR="hists/test_coffea4bees_woHHoutliers_7"
REBIN_DIR="VarRebin"
VARBIN="--variable_binning"
# REBIN_DIR="Rebin"
# VARBIN=" "

config = {
    "rebin": [
        # 2,
        # 3,
        # 4,
        # 5, 
        8,
        10,
        16
    ]
}

rule all:
    input: 
        expand( f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/systs/SvB_MA_ps_hh_fine_nominal.pdf", rebins=config["rebin"] )

rule two_stage_closure:
    output: f"{OUTPUT_DIR}/closureFits/3bDvTMix4bDvT/SvB_MA/{REBIN_DIR.lower()}{{rebins}}/SR/hh/hists_closure_3bDvTMix4bDvT_SvB_MA_ps_hh_fine_{REBIN_DIR.lower()}{{rebins}}.pkl"
    params:
        rebin="{rebins}",
        container = combine_container,
        outputPath=f"{OUTPUT_DIR}/closureFits/"
    shell:
        '''
        source workflows/helpers/shell_combine.sh {params.container} \
            python3 stats_analysis/runTwoStageClosure.py  --var SvB_MA_ps_hh_fine  \
                --rebin {params.rebin} --use_kfold {VARBIN} \
                --outputPath {params.outputPath} \
                --input_file_TT {INPUT_DIR}/histMixedBkg_TT.root \
                --input_file_mix {INPUT_DIR}/histMixedData.root \
                --input_file_sig {INPUT_DIR}/histAll.root \
                --input_file_data3b {INPUT_DIR}/histMixedBkg_data_3b_for_mixed.root
        '''

rule make_combine_inputs:
    input: f"{OUTPUT_DIR}/closureFits/3bDvTMix4bDvT/SvB_MA/{REBIN_DIR.lower()}{{rebins}}/SR/hh/hists_closure_3bDvTMix4bDvT_SvB_MA_ps_hh_fine_{REBIN_DIR.lower()}{{rebins}}.pkl"
    output: 
        f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/datacard_HHbb_2016.txt",
        f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/shapes.root"
    params:
        rebin="{rebins}",
        container = combine_container,
        output=f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}"
    shell:
        '''
        source workflows/helpers/shell_combine.sh {params.container} \
            python3 stats_analysis/make_combine_inputs.py --var SvB_MA.ps_hh_fine \
                -f {INPUT_DIR}/histAll.json {VARBIN} \
                --syst_file {INPUT_DIR}/histAll_signals_cHHHX.json \
                --bkg_syst_file {input} \
                --output_dir {params.output} \
                --rebin {params.rebin} \
                --metadata stats_analysis/metadata/HH4b.yml \
                --mixeddata_file {INPUT_DIR}/histMixedData.json
        '''

rule make_limits:
    input: f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/datacard_HHbb_2016.txt"
    output: 
        f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/datacard.txt"
    params:
        container = combine_container,
        output=f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/"
    shell:
        '''
        source workflows/helpers/shell_combine.sh {params.container} \
            source stats_analysis/run_combine.sh {params.output} --limits
        '''

rule make_syst_plots:
    input: 
        root=f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/shapes.root",
        datacard=f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/datacard.txt"
    output: f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/systs/SvB_MA_ps_hh_fine_nominal.pdf"
    params:
        container = combine_container,
        output=f"{OUTPUT_DIR}/{REBIN_DIR}{{rebins}}/systs/"
    shell:
        """
        source workflows/helpers/shell_combine.sh {params.container} \
            python3 plots/make_syst_plots.py -i {input.root} -o {params.output} -d {input.datacard}
        """